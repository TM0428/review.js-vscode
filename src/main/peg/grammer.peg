Start "start"
    = _ c:Chapters? _
    ;

Chapters "chapters"
    = c:Chapter cc:Chapters?
    ;

Chapter "chapter"
    = headline:Headline text:Paragraphs?
    ;

// = 章タイトル
Headline "headline"
    = level:"="+ label:BracketArg? tag:BraceArg? Space* caption:ContentInline Newline*
    ;

Paragraphs "paragraphs"
    = !"=" c:Paragraph cc:Paragraphs?
    ;

Paragraph "paragraph"
    = Newline* c:Contents Newline?
    ;

Contents "contents"
    // eof 検出に &. を使っている
    = &. !"=" c:Content cc:Contents? Newline?
    ;

Content "content"
    // TODO InlineElement の後に Ulist / Olist / Dlist が来ると先頭行じゃなくてマッチできてしまうかも
    = c:SinglelineComment / c:BlockElement / c:InlineElement / c:Ulist / c:Olist / c:Dlist / c:ContentText
    ;

ContentText "text of content"
    = text:$(!"//}" ( !InlineElement [^\r\n] )+ Newline?)
    ;

BlockElement "block element"
    = "//" name:$(AZ+) args:BracketArg* "{" Newline contents:BlockElementContents? "//}"
    ;

InlineElement "inline element"
    = "@<" name:$([^>\r\n]+) ">" "{" contents:InlineInnerContents? "}"
    ;

BracketArg "bracket argument"
    = "[" arg:$([^\n\]]*) "]"
    ;

BraceArg "brace argument"
    = "{" arg:$([^\n\}]*) "}"
    ;

// contents との差は paragraph を切るか切らないか
BlockElementContents "contents of block element"
    = c:BlockElementContent cc:BlockElementContents?
    ;

BlockElementContent "content of block element"
    = c:SinglelineComment / c:BlockElement / c:InlineElement / c:Ulist / c:Olist / c:Dlist / c:Newline / c:BlockElementContentText
    ;

BlockElementContentText "text of block element"
    = text:$(!"//}" ( !InlineElement [^\r\n] )+)
    ;

InlineInnerContents "contents of inline element"
    = !"}" c:InlineInnerContent cc:InlineInnerContents?
    ;

InlineInnerContent "content of inline element"
    = c:EndOfInlineInnerContent / c:InlineElement / c:InlineInnerContentText
    ;

EndOfInlineInnerContent "end of content of inline element"
    = text:$("\\}")
    ;

InlineInnerContentText "text of inline element"
    = text:$( ( !InlineElement [^\r\n}] )+ )
    ;

ContentInline "inline content"
    = c:ContentInlineHelpers (Newline / EOF)
    ;

ContentInlineHelpers "children of inline content"
    = c:ContentInlineHelper cc:ContentInlineHelpers?
    ;

ContentInlineHelper "child of inline content"
    = c:ContentInlineHelperText / c:InlineElement
    ;

ContentInlineHelperText "text of child of inline content"
    = text:$( ( !InlineElement [^\r\n] )+ )
    ;

// * 箇条書き
Ulist "ulist"
    // 行頭から… の指定がない
    = c:(UlistElement / SinglelineComment) cc:Ulist?
    ;

UlistElement "ulist element"
    = " "+ level:"*"+ _ text:ContentInline
    ;

// 1. 番号付き箇条書き
Olist "olist"
    // 行頭から… の指定がない
    = c:(OlistElement / SinglelineComment) cc:Olist?
    ;

OlistElement "olist element"
    = " "+ n:Digits "." _ text:ContentInline
    ;

// : 用語リスト
Dlist "dlist"
    // 行頭から… の指定がない
    = c:(DlistElement / SinglelineComment) cc:Dlist?
    ;

DlistElement "dlist element"
    // TODO content が text なのは間違っているのでは…
    = " "* ":" " " _ text:ContentInline content:DlistElementText
    ;

DlistElementText "text of dlist element"
    = [ \t]+ text:$( ContentInline )+
    ;

SinglelineComment "signle line comment"
    = text:$("#@" $([^\r\n]*) Newline?)
    ;

Digits "digits"
    = $(Digit+)
    ;

Digit "digit"
    = [0-9]
    ;

AZ "lower alphabet"
    = [a-z]
    ;

Newline "newline"
    = "\r\n"
    / "\n"
    ;

_ "spacer"
    = $(Space*)
    ;

Space "space"
    = [ \t\r\n]
    ;

EOF "end of file"
    = !.
    ;
