Start
    = _ Chapters? _
    ;

Chapters "chapters"
    = Chapter Chapters?

Chapter "chapter"
    = Headline text:Paragraphs?
    ;

// = 章タイトル
Headline "headline"
    // TODO caption は inline element
    = level:"="+ BracketArg? BraceArg? Space* caption:ContentInline Newline*
    ;

Paragraphs "paragraphs"
    = !"=" Paragraph Paragraphs?
    ;

Paragraph "paragraph"
    = Newline* Contents Newline?
    ;

Contents "contents"
    // eof 検出に &. を使っている
    = &. Content Contents? Newline?
    ;

Content "content"
    // TODO InlineElement の後に Ulist / Olist / Dlist が来ると先頭行じゃなくてマッチできてしまうかも
    = ( SinglelineComment / BlockElement / InlineElement / Ulist / Olist / Dlist / content:$(!"//}" [^\r\n@]+) )
    ;

BlockElement "block element"
    = "//" name:$(AZ+) BracketArg* "{" Newline BlockInnerContents? "//}"
    ;

InlineElement "inline element"
    = "@<" name:$([^>\r\n]+) ">" "{" InlineInnerContents? "}"
    ;

BracketArg "bracket argument"
    = "[" arg:$([^\n\]]*) "]"
    ;

BraceArg "brace argument"
    = "{" arg:$([^\n\}]*) "}"
    ;

// contents との差は paragraph を切るか切らないか
BlockInnerContents
    = BlockInnerContent BlockInnerContents?
    ;

BlockInnerContent
    = ( SinglelineComment / BlockElement / InlineElement / Ulist / Olist / Dlist / Newline / content:$(!"//}" [^\r\n@]+) ) 
    ;

InlineInnerContents
    = !"}" InlineInnerContent InlineInnerContents?
    ;

InlineInnerContent
    = "\\}"
    / content:$([^\r\n@}]+)
    / InlineElement
    ;

ContentInline "inline content"
    = ContentInline_subs (Newline / EOF)
    ;

ContentInline_subs "children of inline content"
    = ContentInline_sub ContentInline_subs?
    ;

ContentInline_sub "child of inline content"
    = content:$([^\r\n@]+)
    / InlineElement
    ;

// * 箇条書き
Ulist "ulist"
    // 行頭から… の指定がない
    = UlistElement (SinglelineComment / UlistElement)*
    ;

UlistElement "ulist element"
    = " "+ level:"*"+ _ text:ContentInline
    ;

// 1. 番号付き箇条書き
Olist "olist"
    // 行頭から… の指定がない
    = OlistElement (SinglelineComment / OlistElement)*
    ;

OlistElement "olist element"
    = " "+ Digits "." _ text:ContentInline
    ;

// : 用語リスト
Dlist "dlist"
    // 行頭から… の指定がない
    = DlistElement (SinglelineComment / DlistElement)*
    ;

DlistElement "dlist element"
    = " "* ":" " " _ text:ContentInline content:( [ \t]+ ContentInline )+
    ;

SinglelineComment "signle line comment"
    = "#@" $([^\r\n]*) Newline?
    ;

Digits "digits"
    = Digit+
    ;

Digit "digit"
    = [0-9]
    ;

AZ "lower alphabet"
    = [a-z]
    ;

Newline "newline"
    = "\r\n"
    / "\n"
    ;

_ "spacer"
    = Space*
    ;

Space "space"
    = [ \t\r\n]
    ;

EOF
    = !.
    ;
