Start "start"
    = _ c:Chapters? _
    ;

Chapters "chapters"
    = c:Chapter cc:Chapters?
    ;

Chapter "chapter"
    = headline:Headline text:Paragraphs?
    ;

// = 章タイトル
Headline "headline"
    = level:"="+ label:BracketArg? tag:BraceArg? Space* caption:ContentInline Newline*
    ;

Paragraphs "paragraphs"
    = !"=" c:Paragraph cc:Paragraphs?
    ;

Paragraph "paragraph"
    = Newline* c:Contents Newline?
    ;

Contents "contents"
    // eof 検出に &. を使っている
    = &. !"=" c:Content cc:Contents? Newline?
    ;

Content "content"
    // TODO InlineElement の後に Ulist / Olist / Dlist が来ると先頭行じゃなくてマッチできてしまうかも
    = c:SinglelineComment / c:BlockElement / c:InlineElement / c:Ulist / c:Olist / c:Dlist / c:ContentText
    ;

ContentText "text of content"
    = c:$(!"//}" ( !InlineElement [^\r\n] )+ Newline?)
    ;

BlockElement "block element"
    = "//" name:$(AZ+) args:BracketArg* "{" Newline contents:BlockInnerContents? "//}"
    ;

InlineElement "inline element"
    = "@<" name:$([^>\r\n]+) ">" "{" contents:InlineInnerContents? "}"
    ;

BracketArg "bracket argument"
    = "[" arg:$([^\n\]]*) "]"
    ;

BraceArg "brace argument"
    = "{" arg:$([^\n\}]*) "}"
    ;

// contents との差は paragraph を切るか切らないか
BlockInnerContents "contents of block element"
    = c:BlockInnerContent cc:BlockInnerContents?
    ;

BlockInnerContent "content of block element"
    = c:SinglelineComment / c:BlockElement / c:InlineElement / c:Ulist / c:Olist / c:Dlist / c:Newline / c:BlockInnerContentText
    ;

BlockInnerContentText "text of block element"
    = c:$(!"//}" ( !InlineElement [^\r\n] )+)
    ;

InlineInnerContents "contents of inline element"
    = !"}" c:InlineInnerContent cc:InlineInnerContents?
    ;

InlineInnerContent "content of inline element"
    = c:EndOfInlineInnerContent / c:InlineElement / c:InlineInnerContentText
    ;

EndOfInlineInnerContent "end of content of inline element"
    = c:$("\\}")
    ;

InlineInnerContentText "text of inline element"
    = c:$( ( !InlineElement [^\r\n}] )+ )
    ;

ContentInline "inline content"
    = c:ContentInlineHelpers (Newline / EOF)
    ;

ContentInlineHelpers "children of inline content"
    = c:ContentInlineHelper cc:ContentInlineHelpers?
    ;

ContentInlineHelper "child of inline content"
    = c:ContentInlineHelperText / c:InlineElement
    ;

ContentInlineHelperText "text of child of inline content"
    = c:$( ( !InlineElement [^\r\n] )+ )
    ;

// * 箇条書き
Ulist "ulist"
    // 行頭から… の指定がない
    = c:UlistElement cc:(SinglelineComment / UlistElement)*
    ;

UlistElement "ulist element"
    = " "+ level:"*"+ _ text:ContentInline
    ;

// 1. 番号付き箇条書き
Olist "olist"
    // 行頭から… の指定がない
    = c:OlistElement cc:(SinglelineComment / OlistElement)*
    ;

OlistElement "olist element"
    = " "+ n:Digits "." _ text:ContentInline
    ;

// : 用語リスト
Dlist "dlist"
    // 行頭から… の指定がない
    = c:DlistElement cc:(SinglelineComment / DlistElement)*
    ;

DlistElement "dlist element"
    = " "* ":" " " _ text:ContentInline content:DlistElementText
    ;

DlistElementText "text of dlist element"
    = c:$( [ \t]+ ContentInline )+
    ;

SinglelineComment "signle line comment"
    = c:$("#@" $([^\r\n]*) Newline?)
    ;

Digits "digits"
    = $(Digit+)
    ;

Digit "digit"
    = [0-9]
    ;

AZ "lower alphabet"
    = [a-z]
    ;

Newline "newline"
    = "\r\n"
    / "\n"
    ;

_ "spacer"
    = $(Space*)
    ;

Space "space"
    = [ \t\r\n]
    ;

EOF "end of file"
    = !.
    ;
